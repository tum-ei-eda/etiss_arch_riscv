import "rv_base/RISCVBase.core_desc"
import "rv_base/RV32I.core_desc"
import "rv_base/RVA.core_desc"

InstructionSet tum_ret extends RISCVBase {
    architectural_state {
        signed<32> ETISS_RETURNCODE_NOERROR = 0;
        signed<32> ETISS_RETURNCODE_GENERALERROR = -1;
        signed<32> ETISS_RETURNCODE_RELOADBLOCKS = -2;
        signed<32> ETISS_RETURNCODE_RELOADCURRENTBLOCK = -3;
        signed<32> ETISS_RETURNCODE_DBUS_READ_ERROR = -5;
        signed<32> ETISS_RETURNCODE_DBUS_WRITE_ERROR = -6;
        signed<32> ETISS_RETURNCODE_IBUS_READ_ERROR = -7;
        signed<32> ETISS_RETURNCODE_IBUS_WRITE_ERROR = -8;
        signed<32> ETISS_RETURNCODE_INTERRUPT = -9;
        signed<32> ETISS_RETURNCODE_RESET = -10;
        signed<32> ETISS_RETURNCODE_ILLEGALINSTRUCTION = -11;
        signed<32> ETISS_RETURNCODE_ILLEGALJUMP = -12;
        signed<32> ETISS_RETURNCODE_INSTR_PAGEFAULT = -13;
        signed<32> ETISS_RETURNCODE_LOAD_PAGEFAULT = -14;
        signed<32> ETISS_RETURNCODE_STORE_PAGEFAULT = -15;
        signed<32> ETISS_RETURNCODE_GDBNOERROR = -16;
        signed<32> ETISS_RETURNCODE_SYSCALL = -17;
        signed<32> ETISS_RETURNCODE_PAGEFAULT = -18;
        signed<32> ETISS_RETURNCODE_BREAKPOINT = -19;
        signed<32> ETISS_RETURNCODE_CPUFINISHED = 1 << 31;
        signed<32> ETISS_RETURNCODE_CPUTERMINATED = 0x80000001;
        signed<32> ETISS_RETURNCODE_JITERROR = -32;
        signed<32> ETISS_RETURNCODE_JITCOMPILATIONERROR = -33;
        signed<32> ETISS_RETURNCODE_ARCHERROR = -34;
        signed<32> ETISS_RETURNCODE_EMULATIONNOTSUPPORTED = -35;
        signed<32> ETISS_RETURNCODE_INVALIDSYSTEM = -36;
        signed<32> ETISS_RETURNCODE_TEMPORARY0 = -128;
        signed<32> ETISS_RETURNCODE_TEMPORARY1 = -129;
        signed<32> ETISS_RETURNCODE_TEMPORARY2 = -130;
        signed<32> ETISS_RETURNCODE_TEMPORARY3 = -131;
        signed<32> ETISS_RETURNCODE_TEMPORARY4 = -132;
        signed<32> ETISS_RETURNCODE_TEMPORARY5 = -133;
        signed<32> ETISS_RETURNCODE_TEMPORARY6 = -134;
        signed<32> ETISS_RETURNCODE_TEMPORARY7 = -135;
    }

    functions {
        extern void etiss_icache_flush() [[ etiss_needs_simloop_return ]] [[ etiss_needs_arch ]];

        unsigned<64> get_field(unsigned<64> reg, unsigned<64> mask) {
            return (reg & mask) / (mask & ~(mask << 1));
        }

        unsigned<64> set_field(unsigned<64> reg, unsigned<64> mask, unsigned<64> val) {
            return ((reg & ~mask) | ((val * (mask & ~(mask << 1))) & mask));
        }

        void raise(int irq, int mcause) [[ etiss_trap_entry_fn ]] {
            unsigned<XLEN> epc = PC;

            if (PRIV <= RV_PRV_S && (CSR[RV_CSR_MEDELEG] >> mcause) & 1) { // handle in S-MODE
                PC = (CSR[RV_CSR_STVEC] & ~(unsigned<XLEN>)1);
                CSR[RV_CSR_SEPC] = epc;
                CSR[RV_CSR_SCAUSE] = mcause;

                unsigned<XLEN> s = CSR[RV_CSR_SSTATUS];
                s = set_field(s, RV_MSTATUS_SPIE, get_field(s, RV_MSTATUS_SIE));
                s = set_field(s, RV_MSTATUS_SPP, PRIV);
                s = set_field(s, RV_MSTATUS_SIE, 0);
                CSR[RV_CSR_SSTATUS] = s;
                PRIV = RV_PRV_S;
            } else {    // handle in M-MODE
                PC = (CSR[RV_CSR_MTVEC] & ~(unsigned<XLEN>)1);
                CSR[RV_CSR_MEPC] = epc;
                CSR[RV_CSR_MCAUSE] = mcause;

                unsigned<XLEN> s = CSR[RV_CSR_MSTATUS];
                s = set_field(s, RV_MSTATUS_MPIE, get_field(s, RV_MSTATUS_MIE));
                s = set_field(s, RV_MSTATUS_MPP, PRIV);
                s = set_field(s, RV_MSTATUS_MIE, 0);
                CSR[RV_CSR_MSTATUS] = s;
                PRIV = RV_PRV_M;
            }
        }

        void translate_exc_code(int cause) [[ etiss_trap_translate_fn=MEM ]] {
            int code = 0;

            if (cause == ETISS_RETURNCODE_CPUFINISHED) return;
            else if (cause == ETISS_RETURNCODE_DBUS_READ_ERROR) code = RV_CAUSE_LOAD_ACCESS;
            else if (cause == ETISS_RETURNCODE_INSTR_PAGEFAULT) code = RV_CAUSE_FETCH_PAGE_FAULT;
            else if (cause == ETISS_RETURNCODE_LOAD_PAGEFAULT) code = RV_CAUSE_LOAD_PAGE_FAULT;
            else if (cause == ETISS_RETURNCODE_DBUS_WRITE_ERROR) code = RV_CAUSE_STORE_ACCESS;
            else if (cause == ETISS_RETURNCODE_STORE_PAGEFAULT) code = RV_CAUSE_STORE_PAGE_FAULT;
            else if (cause == ETISS_RETURNCODE_IBUS_READ_ERROR) code = RV_CAUSE_FETCH_ACCESS;
            else code = RV_CAUSE_ILLEGAL_INSTRUCTION;

            raise(0, code);
        }
    }

    instructions {
        MRET [[no_cont]] {
            encoding: 7'b0011000 :: 5'b00010 :: 5'b00000 :: 3'b000 :: 5'b00000 :: 7'b1110011;
            behavior: {
                if (PRIV < RV_PRV_M)
                    raise(0, RV_CAUSE_ILLEGAL_INSTRUCTION);

                PC = CSR[RV_CSR_MEPC];

                unsigned<XLEN> s = CSR[RV_CSR_MSTATUS];

                unsigned<XLEN> prev_prv = get_field(s, RV_MSTATUS_MPP);

                if (prev_prv != RV_PRV_M)
                    s = set_field(s, RV_MSTATUS_MPRV, 0);

                if (prev_prv != PRIV && CSR[RV_CSR_SATP] != 0)
                    etiss_icache_flush();

                s = set_field(s, RV_MSTATUS_MIE, get_field(s, RV_MSTATUS_MPIE));
                s = set_field(s, RV_MSTATUS_MPIE, 1);
                s = set_field(s, RV_MSTATUS_MPP, extension_enabled('U') ? RV_PRV_U : RV_PRV_M);

                CSR[RV_CSR_MSTATUS] = s;
                PRIV = prev_prv;
            }
        }

        SRET [[no_cont]] {
            encoding: 7'b0001000 :: 5'b00010 :: 5'b00000 :: 3'b000 :: 5'b00000 :: 7'b1110011;
            behavior: {
                if (PRIV < (get_field(CSR[RV_CSR_MSTATUS], RV_MSTATUS_TSR) ? RV_PRV_M : RV_PRV_S))
                    raise(0, RV_CAUSE_ILLEGAL_INSTRUCTION);

                PC = CSR[RV_CSR_SEPC];

                unsigned<XLEN> s = CSR[RV_CSR_SSTATUS];

                unsigned<XLEN> prev_prv = get_field(s, RV_MSTATUS_SPP);

                s = set_field(s, RV_MSTATUS_SIE, get_field(s, RV_MSTATUS_SPIE));
                s = set_field(s, RV_MSTATUS_SPIE, 1);
                s = set_field(s, RV_MSTATUS_SPP, RV_PRV_U);

                CSR[RV_CSR_MSTATUS] = s;
                PRIV = prev_prv;
            }
        }
    }
}

InstructionSet tum_csr extends Zicsr {
    functions {
        // black-box function to communicate SATP-changes to MMU
        extern signed<32> ETISS_SIGNAL_MMU(unsigned<XLEN> mmu_signal_) [[etiss_needs_arch]];

        extern unsigned<64> etiss_get_cycles() [[ etiss_needs_arch ]];
        extern unsigned<64> etiss_get_time();
        extern unsigned<64> etiss_get_instret() [[ etiss_needs_arch ]];

        unsigned<XLEN> sstatus_mask() {
            unsigned<XLEN> mask = 0;

            if (extension_enabled('S')) {
                mask |= 0 | (RV_MSTATUS_SIE | RV_MSTATUS_SPIE | RV_MSTATUS_SPP | RV_MSTATUS_MXR | RV_MSTATUS_TVM | RV_MSTATUS_TSR);

                if (extension_enabled('V'))
                    mask |= RV_MSTATUS_VS;

                if (extension_enabled('F'))
                    mask |= RV_MSTATUS_FS;

                if (extension_enabled('X'))
                    mask |= RV_MSTATUS_XS;

                if (XLEN == 32 && get_field(CSR[RV_CSR_SATP], 1 << 31) || XLEN == 64 && get_field(CSR[RV_CSR_SATP], 15 << 60))
                    mask |= RV_MSTATUS_SUM;
            }

            return mask;
        }

        unsigned<XLEN> mstatus_mask() {
            unsigned<XLEN> mask = 0 | (RV_MSTATUS_MIE | RV_MSTATUS_MPIE | RV_MSTATUS_MPP);

            return mask | sstatus_mask();
        }

        unsigned<XLEN> csr_read(unsigned int csr) {
            if (csr == RV_CSR_FFLAGS) return CSR[RV_CSR_FCSR] & 0x1F;
            if (csr == RV_CSR_FRM) return (CSR[RV_CSR_FCSR] >> 5) & 0x07;
            if (csr == RV_CSR_CYCLE) return etiss_get_cycles();
            if (csr == RV_CSR_CYCLEH) return etiss_get_cycles() >> 32;
            if (csr == RV_CSR_TIME) return etiss_get_time();
            if (csr == RV_CSR_TIMEH) return etiss_get_time() >> 32;
            if (csr == RV_CSR_INSTRET) return etiss_get_instret();
            if (csr == RV_CSR_INSTRETH) return etiss_get_instret() >> 32;
            if (csr == RV_CSR_MSTATUS || csr == RV_CSR_SSTATUS) return CSR[RV_CSR_MSTATUS] | (RV_MSTATUS_UXL & (RV_MSTATUS_UXL << 1)) | (RV_MSTATUS_SXL & (RV_MSTATUS_SXL << 1));
            if (csr == RV_CSR_MISA) return (unsigned<2>)(XLEN / 32) :: CSR[RV_CSR_MISA][XLEN - 2:0];

            return CSR[csr];
        }

        void csr_write(unsigned int csr, unsigned<XLEN> val) {
            if (csr == RV_CSR_FFLAGS) CSR[RV_CSR_FCSR] = (CSR[RV_CSR_FCSR] & (0x07 << 5)) | (val & 0x1F);
            else if (csr == RV_CSR_FRM) CSR[RV_CSR_FCSR] = ((val & 0x07) << 5) | (CSR[RV_CSR_FCSR] & 0x1F);
            else if (csr == RV_CSR_FCSR) CSR[RV_CSR_FCSR] = val & 0xFF;
            else if (csr == RV_CSR_MSTATUS) CSR[RV_CSR_MSTATUS] = val & mstatus_mask();
            else if (csr == RV_CSR_SSTATUS) CSR[RV_CSR_MSTATUS] = val & sstatus_mask();

            else if (csr != RV_CSR_MISA) CSR[csr] = val;
            if (csr == RV_CSR_SATP)
                ETISS_SIGNAL_MMU(val);
        }
    }

    instructions {
        CSRRW {
            encoding: csr[11:0] :: rs1[4:0] :: 3'b001 :: rd[4:0] :: 7'b1110011;
            assembly:"{name(rd)}, {csr}, {name(rs1)}";
            behavior: {
                unsigned<XLEN> xrs1 = X[rs1 % RFS];
                if ((rd % RFS) != 0) {
                    unsigned<XLEN> xrd = csr_read(csr);
                    csr_write(csr, xrs1);
                    // make sure Xrd is updated once CSR write succeeds
                    X[rd % RFS] = xrd;
                } else {
                    csr_write(csr, xrs1);
                }
            }
        }

        CSRRS {
            encoding: csr[11:0] :: rs1[4:0] :: 3'b010 :: rd[4:0] :: 7'b1110011;
            assembly:"{name(rd)}, {csr}, {name(rs1)}";
            behavior: {
                unsigned<XLEN> xrd = csr_read(csr);
                unsigned<XLEN> xrs1 = X[rs1 % RFS];
                if (rs1 != 0) csr_write(csr, xrd | xrs1);
                if ((rd % RFS) != 0) X[rd % RFS] = xrd;
            }
        }

        CSRRC {
            encoding: csr[11:0] :: rs1[4:0] :: 3'b011 :: rd[4:0] :: 7'b1110011;
            assembly:"{name(rd)}, {csr}, {name(rs1)}";
            behavior: {
                unsigned<XLEN> xrd = csr_read(csr);
                unsigned<XLEN> xrs1 = X[rs1 % RFS];
                if (rs1 != 0) csr_write(csr, xrd & ~xrs1);
                if ((rd % RFS) != 0) X[rd % RFS] = xrd;
            }
        }

        CSRRWI {
            encoding: csr[11:0] :: zimm[4:0] :: 3'b101 :: rd[4:0] :: 7'b1110011;
            assembly:"{name(rd)}, {csr}, {zimm:#0x}";
            behavior: {
                unsigned<XLEN> xrd = csr_read(csr);
                csr_write(csr, (unsigned<XLEN>)zimm);
                if ((rd % RFS) != 0) X[rd % RFS] = xrd;
            }
        }

        CSRRSI {
            encoding: csr[11:0] :: zimm[4:0] :: 3'b110 :: rd[4:0] :: 7'b1110011;
            assembly:"{name(rd)}, {csr}, {zimm:#0x}";
            behavior: {
                unsigned<XLEN> xrd = csr_read(csr);
                if (zimm != 0) csr_write(csr, xrd | (unsigned<XLEN>)zimm);
                if ((rd % RFS) != 0) X[rd % RFS] = xrd;
            }
        }

        CSRRCI {
            encoding: csr[11:0] :: zimm[4:0] :: 3'b111 :: rd[4:0] :: 7'b1110011;
            assembly:"{name(rd)}, {csr}, {zimm:#0x}";
            behavior: {
                unsigned<XLEN> xrd = csr_read(csr);
                if (zimm != 0) csr_write(csr, xrd & ~((unsigned<XLEN>)zimm));
                if ((rd % RFS) != 0) X[rd % RFS] = xrd;
            }
        }
    }
}

InstructionSet tum_rva extends RV32A {
    architectural_state {
        register unsigned<XLEN> RES_ADDR = -1;
    }

    instructions {
        LRW {
            encoding: 5'b00010 :: aq[0:0] :: rl[0:0]  :: 5'b00000 :: rs1[4:0] :: 3'b010 :: rd[4:0] :: 7'b0101111;
            assembly: "{name(rd)}, {name(rs1)}, {name(aq)}, {name(rl)}";
            behavior: {
                unsigned<XLEN> offs = X[rs1 % RFS];
                signed<32> res = (signed<32>)MEM[offs];
                RES_ADDR = offs;
                if (rd) X[rd % RFS] = (signed<XLEN>)res;
            }
        }

        SCW {
            encoding: 5'b00011 :: aq[0:0] :: rl[0:0] :: rs2[4:0] :: rs1[4:0] :: 3'b010 :: rd[4:0] :: 7'b0101111;
            assembly: "{name(rd)}, {name(rs1)}, {name(rs2)}, {name(aq)}, {name(rl)}";
            behavior: {
                unsigned<XLEN> offs = X[rs1 % RFS];
                if (RES_ADDR == offs) MEM[offs] = (signed<32>)X[rs2 % RFS];
                if (rd) X[rd % RFS] = RES_ADDR != offs;
                RES_ADDR = -1;
            }
        }
    }
}

InstructionSet tum_rva64 extends tum_rva {
    instructions {
        LRD {
            encoding: 5'b00010 :: aq[0:0] :: rl[0:0]  :: 5'b00000 :: rs1[4:0] :: 3'b011 :: rd[4:0] :: 7'b0101111;
            assembly: "{name(rd)}, {name(rs1)}, {name(aq)}, {name(rl)}";
            behavior: {
                unsigned<XLEN> offs = X[rs1 % RFS];
                signed<64> res = (signed<64>)MEM[offs];
                RES_ADDR = offs;
                if (rd) X[rd % RFS] = (signed<XLEN>)res;
            }
        }

        SCD {
            encoding: 5'b00011 :: aq[0:0] :: rl[0:0] :: rs2[4:0] :: rs1[4:0] :: 3'b011 :: rd[4:0] :: 7'b0101111;
            assembly: "{name(rd)}, {name(rs1)}, {name(rs2)}, {name(aq)}, {name(rl)}";
            behavior: {
                unsigned<XLEN> offs = X[rs1 % RFS];
                if (RES_ADDR == offs) MEM[offs] = (signed<64>)X[rs2 % RFS];
                if (rd) X[rd % RFS] = RES_ADDR != offs;
                RES_ADDR = -1;
            }
        }
    }
}

InstructionSet tum_semihosting extends RV32I {
    instructions {
        EBREAK [[no_cont]] [[cond]] {
            encoding: 0b000000000001 :: 5'b00000 :: 3'b000 :: 5'b00000 :: 7'b1110011;
            behavior: {
                if (etiss_semihost_enabled()) {
                    unsigned<32> pre    = (unsigned<32>)MEM[PC - 4];
                    unsigned<32> ebreak = (unsigned<32>)MEM[PC + 0];
                    unsigned<32> post   = (unsigned<32>)MEM[PC + 4];
                    if ( pre    == 0x01f01013 // slli x0, x0, 0x1f   Entry NOP
                      && ebreak == 0x00100073 // ebreak              Break to debugger
                      && post   == 0x40705013 // srai x0, x0, 7      NOP encoding the semihosting call number 7
                    ) {
                    unsigned<XLEN> operation = X[10];
                    unsigned<XLEN> parameter = X[11];
                    X[10] = (signed<XLEN>)etiss_semihost(XLEN, operation, parameter);
                    } else {
                        raise(0, RV_CAUSE_BREAKPOINT);
                    }
                } else {
                    raise(0, RV_CAUSE_BREAKPOINT);
                }
            }
        }
    }

    functions {
        extern unsigned<8> etiss_semihost_enabled();
        extern signed<64> etiss_semihost(unsigned<32> XLEN, unsigned<64> operation, unsigned<64> parameter) [[etiss_needs_arch]];
    }
}

InstructionSet tum_sfence extends RV32I {
    functions {
        extern signed<32> ETISS_TLB_FLUSH() [[etiss_needs_arch]];
        extern signed<32> ETISS_TLB_EVICT_VMA(unsigned<64> vma_) [[etiss_needs_arch]];

        signed<32> evict_all() {
            return ETISS_TLB_FLUSH();   // evicts all addresses from all asids
        }

        signed<32> evict_asid(unsigned<XLEN> asid) {
            return ETISS_TLB_FLUSH();   // TODO: should only evict all addresses with given asid
        }

        signed<32> evict_addr(unsigned<XLEN> vaddr) {
            return ETISS_TLB_EVICT_VMA(vaddr);  // evicts given address from all asids
        }

        signed<32> evict_addr_asid(unsigned<XLEN> vaddr, unsigned<XLEN> asid) {
            return ETISS_TLB_FLUSH();   // TODO: should only evict given address in given asid
        }
    }

    instructions {
        SFENCE_VMA [[ flush ]] {
            encoding: 0b0001001 :: rs2[4:0] :: rs1[4:0] :: 0b000 :: 0b00000 :: 0b1110011;
            args_disass: "{name(rs1)}, {name(rs2)}";
            behavior: {
                FENCE[2] = rs1;
                FENCE[3] = rs2;
                unsigned<XLEN> vaddr = X[rs1];
                unsigned<XLEN> asid = X[rs2];
                if (rs1 == 0) {
                    if (rs2 == 0) evict_all();
                    else evict_asid(asid);
                }
                else {
                    if (rs2 == 0) evict_addr(vaddr);
                    else evict_addr_asid(vaddr, asid);
                }
            }
        }
    }
}